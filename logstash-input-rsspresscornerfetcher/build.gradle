import java.nio.file.Files
import static java.nio.file.StandardCopyOption.REPLACE_EXISTING

apply plugin: 'java'
apply from: LOGSTASH_CORE_PATH + "/../rubyUtils.gradle"
apply plugin: "eclipse"

// ===========================================================================
// plugin info
// ===========================================================================
group                      'eu.wajja.rss.presscorner.fetcher' // must match the package of the main plugin class
version                    "${file("VERSION").text.trim()}" // read from required VERSION file
description                = "Logstash RSS Presscorner Fetcher"
pluginInfo.licenses        = ['Apache-2.0'] // list of SPDX license IDs
pluginInfo.longDescription = "This gem is a Logstash plugin required to be installed on top of the Logstash core pipeline using \$LS_HOME/bin/logstash-plugin install gemname. This gem is not a stand-alone program"
pluginInfo.authors         = ['Mahy Tom']
pluginInfo.email           = ['mail@tommahy.com']
pluginInfo.homepage        = "http://#"
pluginInfo.pluginType      = "input"
pluginInfo.pluginClass     = "RssPressCornerFetcher"
pluginInfo.pluginName      = "rsspresscornerfetcher" // must match the @LogstashPlugin annotation in the main plugin class
// ===========================================================================


sourceCompatibility = 1.8
targetCompatibility = 1.8

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.4'
    }
}

repositories {
    mavenCentral()
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.0'
}

apply plugin: 'com.github.johnrengelman.shadow'

shadowJar {
    classifier = null
}

dependencies {
    compile 'org.apache.commons:commons-lang3:3.7'
    compile group: 'org.jsoup', name: 'jsoup', version: '1.12.1'
	compile group: 'commons-io', name: 'commons-io', version: '2.6'
    compile group: 'org.seleniumhq.selenium', name: 'selenium-chrome-driver', version: '4.0.0-alpha-3'
   	compile group: 'com.machinepublishers', name: 'jbrowserdriver', version: '1.1.0-RC2'
   	compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.0.1'
   	compile group: 'org.quartz-scheduler', name: 'quartz', version: '2.3.2'
   	compile group: 'org.codehaus.janino', name: 'janino', version: '3.1.0'
   	
   	compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.13.3'
	compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.13.3'
	compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
   	compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.26'
   	
	compile group: 'org.elasticsearch.client', name: 'elasticsearch-rest-high-level-client', version: '6.8.8'
   	
    compile fileTree(dir: LOGSTASH_CORE_PATH, include: "**/logstash-core-?.?.?.jar")

    testCompile 'junit:junit:4.12'
    testCompile group: 'net.bytebuddy', name: 'byte-buddy-agent', version: '1.10.14'
    testCompile 'org.jruby:jruby-complete:9.1.13.0'
    testImplementation 'org.mockito:mockito-inline:2.23.4'
}

clean {
    delete "${projectDir}/Gemfile"
    delete "${projectDir}/" + pluginInfo.pluginFullName() + ".gemspec"
    delete "${projectDir}/lib/"
    delete "${projectDir}/vendor/"
    new FileNameFinder().getFileNames(projectDir.toString(), pluginInfo.pluginFullName() + "-?.?.?.gem").each { filename ->
        delete filename
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

task vendor(dependsOn: shadowJar) {
    doLast {
        String vendorPathPrefix = "vendor/jar-dependencies"
        String projectGroupPath = project.group.replaceAll('\\.', '/')
        File projectJarFile = file("${vendorPathPrefix}/${projectGroupPath}/${pluginInfo.pluginFullName()}/${project.version}/${pluginInfo.pluginFullName()}-${project.version}.jar")
        projectJarFile.mkdirs()
        Files.copy(file("$buildDir/libs/${project.name}-${project.version}.jar").toPath(), projectJarFile.toPath(), REPLACE_EXISTING)
        validatePluginJar(projectJarFile, project.group)
    }
}

task generateRubySupportFiles() {
    doLast {
        generateRubySupportFilesForPlugin(project.description, project.group, version)
    }
}

task removeObsoleteJars() {
    doLast {
        new FileNameFinder().getFileNames(
                projectDir.toString(),
                "vendor/**/" + pluginInfo.pluginFullName() + "*.jar",
                "vendor/**/" + pluginInfo.pluginFullName() + "-" + version + ".jar").each { f ->
            delete f
        }
    }
}

task gem(dependsOn: [downloadAndInstallJRuby, removeObsoleteJars, vendor, generateRubySupportFiles]) {
    doLast {
        buildGem(projectDir, buildDir, pluginInfo.pluginFullName() + ".gemspec")
    }
}
